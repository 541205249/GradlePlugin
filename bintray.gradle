group = "com.jiazy.demo"
version = "0.0.1"
project.archivesBaseName = "tojcenterdemo"

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        title "tojcenterdemo"
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "541205249"
            name "贾宗艺"
            email "541205249@qq.com"
        }
    }

    /*
    //需要注意的是如果你的项目依赖了第三方库，则你也需要加入，比如这里我引用了fastjson
    //这里有引号，并且有个空格，否则生成不了依赖，
    // 主要是这里的dependencies并不是build.gradle里的dependencies，
    // 而gradle会把他当成自身的dependencies处理
    "dependencies " {
        dependency{
            groupId "com.alibaba"
            artifactId "fastjson"
            "version " "1.2.6"
        }
    }
    */

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "tojcenterdemo"

            pom{
                packaging 'aar'
            }
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "!!demo")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty('BINTRAY_USER')
    key = properties.getProperty('BINTRAY_KEY')

    configurations = ['archives']
    publications = ['mavenJava']
    publish = true

    pkg {
        repo = 'maven'
        name = "tojcenterdemo"
        desc = "gradle插件demo"
        websiteUrl = "https://github.com/541205249/GradlePlugin"
        issueTrackerUrl = "https://github.com/541205249/GradlePlugin/issues"
        vcsUrl = "https://github.com/541205249/GradlePlugin.git"
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}